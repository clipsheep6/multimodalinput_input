# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/test.gni")
import("//foundation/multimodalinput/input/multimodalinput_mini.gni")

if (input_feature_uevent) {
  config("libuevent_config") {
    visibility = [ ":*" ]

    include_dirs = [
      "include",
      "${mmi_path}/util/common/include",
    ]

    cflags = [
      "-DOHOS_BUILD",
      "-DOHOS_BUILD_ENABLE_UEVENT",
    ]
  }

  config("libuevent_public_config") {
    include_dirs = [ "export_include" ]
    cflags = []
  }

  ohos_shared_library("libuevent") {
    part_name = "input"

    configs = [ ":libuevent_config" ]

    public_configs = [ ":libuevent_public_config" ]

    sources = [
      "src/device.cpp",
      "src/device_manager.cpp",
      "src/discriminator.cpp",
      "src/enumerator.cpp",
      "src/monitor.cpp",
      "src/utility.cpp",
      "udev/libudev.cpp",
    ]

    deps = [ "${mmi_path}/util:libmmi-util" ]

    public_deps = [ "//third_party/libevdev:libevdev" ]

    external_deps = [
      "hiviewdfx_hilog_native:libhilog",
      "c_utils:utils",
    ]
  }

  import("//build/test.gni")
  module_output_path = "multimodalinput/unit_out"

  ohos_unittest("UeventTest") {
    module_out_path = module_output_path
    include_dirs = [
      "${mmi_path}/interfaces/native/innerkits/common/include",
      "${mmi_path}/util/common/include",
    ]

    sources = [ "test/uevent_test.cpp" ]

    defines = [ "OHOS_BUILD" ]

    if (input_feature_debug) {
      cflags += [ "-DOHOS_BUILD_MMI_DEBUG" ]
    }

    deps = [
      "//third_party/googletest:gmock_main",
      "//third_party/googletest:gtest_main",
    ]

    external_deps = [
      "hiviewdfx_hilog_native:libhilog",
      "c_utils:utils",
    ]
  }
}
